# Red collect-values and collect-words functions

**Gist ID:** aa5615f051ca1b553c952ed460841e02
**Created:** 2016-07-08T06:38:00Z
**Last updated:** 2018-02-11T04:59:51Z

## File: `collect-values.red`

```Red
collect-values: function [
	"Collect values in a block, by type or custom parse rule"
	block [block!]
	rule "Datatype, prototype value, or parse rule"
	/deep "Include nested blocks"
	/local v
][
	rule: switch/default type?/word rule [
		datatype! [reduce [rule]]	; Turn a plain datatype into a parse rule for that type.
		block! typeset! [:rule]		; Blocks and typesets (e.g. any-word!) work directly as rules.
	][reduce [type? rule]]			; Turn a prototype value into a rule for that value's type.

	; If they didn't spec /deep, any-block! skips nested blocks.
	; /deep does *not* look into nested path or string values.
	;!! We need good examples for `parse into` and its limitations.
	deep: either deep [[any-path! | any-string! | into top-rule]] [any-block!]

	collect [
		parse block top-rule: [
			any [set v rule (keep/only v) | deep | skip]
		]
	]
]
blk: [1 a 2 'b 3 c: 4 :d [a 'b c: :d  E 'F G: :H]]
print mold collect-values blk any-word!
print mold collect-values/deep blk any-word!
print mold collect-values blk set-word! 
print mold collect-values blk [set-word! | get-word!]
print mold collect-values/deep blk [set-word! | get-word!]
print mold collect-values/deep blk first [a:]
print mold collect-values/deep blk integer!
blk: [a/b/c 'j/k/l x/y/z: [d/e/f 'g/h/i t/u/v:]]
print mold collect-values blk path!
print mold collect-values blk lit-path!
print mold collect-values blk set-path!
print mold collect-values/deep blk path!
print mold collect-values/deep blk lit-path!
print mold collect-values/deep blk set-path!
blk: [[a] [b] (c) [[d] (e) ([f])]]
print mold collect-values blk block!
print mold collect-values/deep blk block!
print mold collect-values blk paren!
print mold collect-values blk first [()]
print mold collect-values/deep blk paren!
print mold collect-values blk any-block!
blk: [1 2.0 "b" %file a 3x3 [4.4.4.4 #5 50%]]
print mold collect-values blk [number! | tuple!]
print mold collect-values/deep blk [number! | tuple!]


collect-words: function [
	"Collect words used in a block"
	block [block!]
	/deep "Include nested blocks"
	/set  "Collect set-words only"
][
	word-rule: either set [set-word!][any-word!]
	unique either deep [
		collect-values/deep block word-rule
	][
		collect-values block word-rule
	]
]
blk: [1 a 2 'b 3 c: 4 :d [a 'b c: :d  E 'F G: :H]]
print mold collect-words blk
print mold collect-words/deep blk
print mold collect-words/set blk
print mold collect-words/deep/set blk

```

## Comments

### @dsgeyser on 2016-11-07T06:27:48Z

Hi Gregg. Ifind this to be of great value.

