# A different approach to a cycle generator, using a closure.

**Gist ID:** c0fe722deffb7b448286b5a819e0ca2b
**Created:** 2017-03-21T15:40:27Z
**Last updated:** 2017-08-15T15:33:33Z

## File: `closure-cycler.red`

```Red
Red []

closure: func [
    vars [block!] "Values to close over, in spec block format"
    spec [block!] "Function spec for closure func"
    body [block!] "Body of closure func; vars will be available"
][
    func spec bind body context vars
]
cycler: func [block [block!]][
	closure compose/only [block: (block)] [/reset] [
		if any [reset  tail? block] [block: head block]
		also  first block  block: next block
	]
]
test-cyc: func [cyc-fn [function!]][loop 7 [probe cyc-fn]]
cyc: cycler [1 2 3 4 5]
test-cyc :cyc
cyc: cycler [a #test 5x5 8 "key" %file 10%]
test-cyc :cyc
cyc: cycler [[a #test] [5x5 8] ["key" %file 10%]]
test-cyc :cyc
```

## Comments

No comments.
