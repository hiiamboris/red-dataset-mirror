# Basic Red menu demo

**Gist ID:** f3ba394221b2c55c21a420400f0e11de
**Created:** 2016-08-19T20:44:40Z
**Last updated:** 2017-03-18T18:23:06Z

## File: `menu.red`

```Red
Red [
	File:   %menu.red
	Author: "Gregg Irwin"
]

; The menu dialect in Red is easy. When making a face, you use `menu:`
; followed by a block of [string! block!] pairs, where the string is the
; text of the top level menu and the block is a simple dialect that 
; defines the drop-down menu items. Each item can be either a string!
; followed by a word!, or the special separator marker of '---. That's it.
;
; When a menu item is selected, the `on-menu` actor is called, with
; `event/picked` being set to the word that followed the menu item string.
; All you have to do is dispatch based on that. For example: 
; `switch event/picked [...]`
;
; There is no accelerator key support in menus yet. To handle them, you
; need to use the `on-key` handler separately. To display the accelerator
; key text in the menu item, just put the text you want after a tab character
; in the menu item text.
 
win: make face! [
	type: 'window text: "Red View" size: 640x400
	menu: [
		"File" [
			"New"				new
			"Open...	F1"		open
			"Close	F2"			close 
			---
			"Save..."			save
			"Save as..."		save-as
			"Save All"			save-all
			---
			"Print..."			print
			"Preview"			preview
			"Page Setup..."		page-setup
			---
			"Exit"				exit
		]
		"Edit" [
			"Undo"				undo
			"Redo"				redo
			---
			"Copy	Ctrl+C"		copy
			"Cut	Ctrl+X"		cut
			"Paste	Ctrl+V"		paste
			---
			"Sub1" [
				"Sub-menu1"		sub-m1
			]
			"Sub2" [
				"Sub-menu2"		sub-m2
			]
		]
		"Search" [
			"Find..."			find
			"Find Next"			find-next
			"Find Previous"		find-prev
			---
			"Replace..."		replace
			"Replace Next"		replace-next
		]
		"Deep" [
			"Item 2"			item2
			"Deep 2" [
				"Item 3"		item3
				"Deep 3" [
					"End"		end
				]
			]
		]
	]
	actors: object [
		on-menu: func [face [object!] event [event!]][
			print ["menu selected:" event/picked]
			switch/default event/picked [
				exit [quit]
			][
				win/pane/1/text: form event/picked
			]
		]
		on-key: func [face [object!] event [event!]][
			;print [mold event/key event/ctrl? event/shift?]
			if find [F1 F2] event/key [
				win/pane/1/text: rejoin [event/key " key caught by window face!"]
				'stop
			]
			if all [event/ctrl?  find "CXV" event/key] [
				win/pane/1/text: rejoin ["Ctrl+" event/key " key caught by window face!"]
				'stop
			]
		]
	]
]

win/pane: reduce [
	make face! [
		type: 'text
		text: "Selecting a menu item will change this text"
		offset: 50x50
		size: 200x120
	]
]

view win

```

## Comments

### @greggirwin on 2016-08-19T20:45:01Z

Based on Nenad's menu test code.


