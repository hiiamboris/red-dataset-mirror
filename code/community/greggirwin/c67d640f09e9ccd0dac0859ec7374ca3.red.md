# VID drop-down style demo

**Gist ID:** c67d640f09e9ccd0dac0859ec7374ca3
**Created:** 2017-07-13T18:10:11Z
**Last updated:** 2017-07-22T10:58:08Z

## File: `drop-down-demo.red`

```Red
Red [
	title:  "Drop-down demo"
	author: "Gregg Irwin"
]

; The data facet accepts arbitrary values, but only string values will be added
; to the list and displayed. Extra, non-string values can be used to create
; associative arrays, using strings as keys. The `selected` facet is a 1-based
; integer index, indicating the position of the selected string in the list,
; not in the `data` facet.

show-action: func [face type][
	print [
		now/time type
		"selected:" face/selected
		"text:"  face/text
		"find*:" mold select face/data face/text
		"pick*:" mold pick face/data face/selected * 2
	]
]

view [
	drop-down
		data ["Alpha" #1 "Beta" 2x2 "Gamma" #"C"]
		select 2                                        ; Set default list item
		on-select [show-action face 'select]
		on-change [show-action face 'change]
	drop-down
		data [#4 "Delta" 5x5 "Epsilon" %"After Epsilon" #"F" "Zeta"]
		on-select [show-action face 'select]
		on-change [show-action face 'change]
	drop-down
		data ["A" "B" "C" "D" "E" "F"]
		on-select [show-action face 'select]
		on-change [show-action face 'change]
]

```

## Comments

### @greggirwin on 2017-07-21T20:33:58Z

Updated to show more behavior with non-string data.

