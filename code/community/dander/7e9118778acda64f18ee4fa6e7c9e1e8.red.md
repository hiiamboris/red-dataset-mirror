# lazy range enumeration

**Gist ID:** 7e9118778acda64f18ee4fa6e7c9e1e8
**Created:** 2017-04-26T06:04:20Z
**Last updated:** 2017-04-26T08:02:35Z

## File: `range-generator.red`

```Red
Red [
    Title: "range generator attempt (4?)"
]

; @JacobGood's closure func
closure: func [
	vars [block!] 
	spec [block!] 
	body [block!]
][
	; Can't use `function` here, because it will collect set-words
	; in the body, which may be closure vars.
	func spec compose [(bind body context vars)]
]

range: function [start finish][
    c: context [
        cur: start - 1
        f: finish
        iter: func [][
            either cur < f [
                cur: cur + 1
            ][ none ]
        ]
    ]
    :c/iter
]

range2: function [start finish][
    closure compose [i: (start) finish: (finish)] [] [
        either i <= finish [
            also i
            i: i + 1
        ][none]
    ]
]

..: make op! :range2

for-gen: func [
    'word [word!]
    gen [function!]
    body [block!]
][
    while reduce [to-set-word :word 'gen] body
]

for-gen i 5 .. 9 [print i]
```

## Comments

No comments.
