# Very simple assertion framework to test RED scripts.

**Gist ID:** 379b92ef9ec9e3d553e8a6646996d2b0
**Created:** 2016-04-08T12:07:14Z
**Last updated:** 2016-12-14T05:48:03Z

## File: `assert.red`

```Red
RED [
	Title: "Simple assertion framework to test RED scripts"
	Author: "Fran√ßois Vanzeveren"
	File: %assert.red
]

assert: context [
	_results: none
	_file: none
	
	init: func [
		"Initialize assertion framework."
		/file f [file!] "File in which assertion report will be written"
		/reset {Clear the content of the output file. If not set, the report 
				is appended to the end of the existing file.}
	] [
		_results: copy []
		_file: either file [f] [none]
		if all [f reset] [write f ""]
	]
	
	do: func [
		"Assert that condition is true"
		conditions [block!]
		/local mcond
	] [
		mcond: mold conditions
		unless system/words/do conditions [
			append _results append copy "** Assertion failed for: " mcond
		]
	]
	
	report: func [
		"Report failed assertions."
		/local assert_passed assert_failed
	] [
		either none? _file [
			; Sortie sur la console
			assert_passed: [print "OK! All assertions successfully pass!"]
			assert_failed: [forall _results [print first _results]]
		] [
			; sortie dans fichier
			assert_passed: [write/append _file "OK! All assertions successfully pass!^/"]
			assert_failed: [forall _results [write/append/lines _file append first _results #"^/"]]
		]
		either empty? _results 	[system/words/do assert_passed] 
								[system/words/do assert_failed ]
	]
]
```

## Comments

No comments.
