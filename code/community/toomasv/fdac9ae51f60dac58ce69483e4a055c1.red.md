# 

**Gist ID:** fdac9ae51f60dac58ce69483e4a055c1
**Created:** 2018-02-02T17:42:23Z
**Last updated:** 2018-02-02T22:51:15Z

## File: `z-order.red`

```Red
Red [Purpose: {Study of z-ordering}]
context [
	colors: exclude extract load help-string tuple! 2 [glass transparent snow black]
	ords: collect [repeat i 4 [
		keep compose [at (as-pair d: i - 1 * 20 d) ord (take random colors) (to-string i) 'top 'left]
	]]
	adjust-offsets: func [pane][forall pane [pane/1/offset: (index? pane) - 1 * 20 * 1x1]]
	view compose [
		style ord: base with [
			menu: [
				"Front" front 
				"Forward" forward
				"Move at ..." ["1" one "2" two "3" three "4" four] 
				"Backward" backward
				"Back"	back
				"Index?" idx
			]
			actors: object [
				on-menu: func [face event /local pane found os][
					switch event/picked [
						front 		[move found: find pane: face/parent/pane face tail pane
									 forall found [found/1/offset: (index? found) - 1 * 20 * 1x1]]
						forward 	[move pane: find face/parent/pane face skip pane 1
									 unless 4 = index? pane [
										os: pane/1/offset pane/1/offset: pane/2/offset pane/2/offset: os]
									]
						one 		[move find pane: face/parent/pane face at pane 1 adjust-offsets pane]
						two 		[move find pane: face/parent/pane face at pane 2 adjust-offsets pane]
						three 		[move find pane: face/parent/pane face at pane 3 adjust-offsets pane]
						four 		[move find pane: face/parent/pane face at pane 4 adjust-offsets pane]
						backward 	[move pane: find face/parent/pane face skip pane -1
									 unless 1 = index? pane [
										os: pane/-1/offset pane/-1/offset: pane/1/offset pane/1/offset: os]
									]
						back 		[move find pane: face/parent/pane face head pane adjust-offsets pane]
						idx			[probe index? find face/parent/pane face]
					]
				]
			]
		]
		(ords)
	]
]

```

## Comments

No comments.
