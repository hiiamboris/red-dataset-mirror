# 

**Gist ID:** 2e4bb07205b7c6f92b7ef1b952970641
**Created:** 2016-06-15T04:39:05Z
**Last updated:** 2016-06-17T03:19:23Z

## File: `sparks.red`

```Red
Red [
	Title:   "Sparks demo"
	Author:  "Qingtian Xie"
	File: 	 %sparks.red
	Tabs:	 4
	Needs:	 View
]

system/view/auto-sync?: no

sparks: make block! 200
one-sixty: 1.0 / 60.0

view [
	canvas: base 640x480 "Move the mouse ;-)" all-over rate 60
	on-time [
		draw: clear []
		foreach spark sparks [
			spark/1: spark/1 + (spark/2 / 2)
			spark/2/y: spark/2/y + 3
			spark/3: spark/3 - one-sixty
			reduce/into ['line spark/1 spark/1 + spark/2] tail draw
		]
		canvas/draw: draw
		show canvas
		forall sparks [					;-- remove old spark
			if sparks/1/3 < 0.0 [sparks: back remove sparks]
		]
	][sparks one-sixty]
	on-over [
		append/only sparks reduce [		;@@ optimize the memory usage
			event/offset
			as-pair (20 * random 1.0) - 10 (-40 * random 1.0)
			1.0
		]
	]
]
```

## Comments

No comments.
