# Sparks with a twist

**Gist ID:** 145cccbe09723af58235fc3fecb9ea29
**Created:** 2016-06-15T10:26:18Z
**Last updated:** 2016-06-20T14:55:20Z

## File: `sparks.red`

```Red
Red [
	Title:   "Sparks demo"
	Author:  "Qingtian Xie"
	File: 	 %sparks.red
	Tabs:	 4
	Needs:	 View
]

system/view/auto-sync?: no

font-A: make font! [
	name: "Times New Roman"
	size: 20
	color: red
	style: [bold]
	anti-alias?: yes
]

sparks: make block! 200
one-sixty: 1.0 / 60.0

view [
	bg-canvas: base 200.200.200 640x480 top "Move the mouse ;-)"
	at 0x0
	canvas: image %sparks-red-logo-tp.png 640x480 all-over rate 0:0:0.01666
	on-time [
		draw: copy [pen red ]
		foreach spark sparks [
			spark/1: spark/1 + (spark/2 / 2)
			spark/2/y: spark/2/y + 3
			spark/3: spark/3 - one-sixty
			reduce/into ['line spark/1 spark/1 + spark/2] tail draw
		]
		bg-canvas/draw: draw
		show bg-canvas
		forall sparks [					;-- remove old spark
			if sparks/1/3 < 0.0 [sparks: back remove sparks]
		]
	][sparks one-sixty]
	on-over [
		append/only sparks reduce [		;@@ optimize the memory usage
			event/offset
			as-pair (20 * random 1.0) - 10 (-40 * random 1.0)
			1.0
		]
	]
	do [bg-canvas/font: font-A]
]

system/view/auto-sync?: yes
```

## Comments

### @iArnold on 2016-06-20T14:53:06Z

That is why I wanted to know if the code from the Wiki can be adapted to draw on a transparent background. Now I had to use some tool on the web to get transparency.


### @iArnold on 2016-06-20T14:53:29Z

Could not just add this picture to the gist 


