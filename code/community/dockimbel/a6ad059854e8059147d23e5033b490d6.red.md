# Dynamic View usage example: drawing a board of buttons

**Gist ID:** a6ad059854e8059147d23e5033b490d6
**Created:** 2017-07-08T06:45:36Z
**Last updated:** 2017-07-08T15:52:09Z

## File: `dyn-board.red`

```Red
Red [
	Title:	"Dynamic View usage example"
	Author:	"Nenad Rakocevic"
	Date:	08/07/2017
]

generate: function [board [pair!] sz [pair!] return: [block!]][
	pane: make block! sz/x * sz/y
	pos: 1x1
	count: 1
	loop board/y [
		loop board/x [
			append pane make face! [type: 'button size: sz offset: pos text: form count]
			pos/x: pos/x + sz/x
			count: count + 1
		]
		pos: as-pair 0 pos/y + sz/y
	]
	pane
]

board: 4x5		;-- board's columns x rows
but-sz: 50x50		;-- size of each piece

view center-face make face! [
	type: 'window
	size: as-pair board/x * but-sz/x board/y * but-sz/y
	pane: generate board but-sz
]
```

## Comments

No comments.
