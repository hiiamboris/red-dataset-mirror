# Toy Parse dialect implementation in Red

**Gist ID:** 1538d70b7f54f4fa64d0
**Created:** 2016-02-02T06:19:02Z
**Last updated:** 2016-12-08T21:33:09Z

## File: `simple-parse.red`

```Red
Red [
	Title: "Toy Parse implementation in Red"
	Author: "Nenad Rakocevic"
]

simple-parse: function [input [string!] blk [block!] /sub return: [logic!]][
	while [not tail? blk][				;-- process the rule content
		cmd: blk/1
		match?: no
		
		switch type?/word cmd [
			word! [
				switch cmd [
					thru [
						if pos: find/tail input blk/2 [
							input: pos
							match?: yes
							blk: skip blk 2
						]
					]
					to [
						if pos: find input blk/2 [
							input: pos
							match?: yes
							blk: skip blk 2
						]
					]
					;... more commands here
				]
			]
			string! [
				if pos: find/match input cmd [
					input: pos
					match?: yes
					blk: next blk
				]
			]
			block! [
				set [input match?] simple-parse/sub input cmd ;-- recurse for sub-rules
				blk: next blk			
			]
			;... more datatypes here
		]
		if all [not match? not blk: find/tail blk '|][return false]
	]
	either sub [reduce [input tail? blk]][tail? input]
]


probe simple-parse "This is Red" ["this " "is" " red"]
probe simple-parse "This was Red" ["this " ["is" | "was"] " red"]
probe simple-parse "This is Red" [thru "red"]
probe simple-parse "This is Red" [to "red" "red" ]
```

## Comments

No comments.
