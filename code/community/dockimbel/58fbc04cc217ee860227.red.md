# Simple example of user-defined infix function (includes a very simple FOR implementation).

**Gist ID:** 58fbc04cc217ee860227
**Created:** 2015-10-24T05:13:19Z
**Last updated:** 2015-10-24T05:27:25Z

## File: `infix.red`

```Red
Red [
  Author: "Nenad Rakocevic"
  File: %infix.red
  Note: {
  	Simple example of user-defined infix function.
  	(includes a very simple FOR implementation)
  	
  	Run it from the interpreter, the compiler does not support it as-is.
  	Replacing `cond` by `[do cond]` would make it pass compilation.
  	The compiler should be able to handle such cases in the close future.
  }
]

for: func ['word [word!] start [integer!] bump [integer!] cond [block!] body [block!]][
	set word start
	while cond [
		do body
		set word (get word) + bump
	]
]

make-range: function [a b][
	list: make block! b - a
	for i a 1 [i <= b][append list i]
	list
]

->: make op! :make-range

probe 2 -> 8
;; == [2 3 4 5 6 7 8]
```

## Comments

No comments.
