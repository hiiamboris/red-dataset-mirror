# Simple AREA widget extension for scrolling control using Red/System.

**Gist ID:** dbc310cd996790bf5ab665045c05611e
**Created:** 2016-05-14T09:40:46Z
**Last updated:** 2016-05-14T09:40:46Z

## File: `area-scroll.red`

```Red
Red [Needs: 'View]

#system [
	#import [
		"User32.dll" stdcall [
			GetScrollRange: "GetScrollRange" [
				hWnd	 [handle!]
				nBar	 [integer!]
				lpMinPos [int-ptr!]
				lpMaxPos [int-ptr!]
				return:	 [logic!]
			]
			SetScrollPos: "SetScrollPos" [
				hWnd	[handle!]
				nBar	[integer!]
				nPos	[integer!]
				bRedraw [logic!]
				return: [integer!]
			]
		]
	]
]

scroll-top: routine [
	face [object!]
	/local
		hWnd [handle!]
][
	hWnd: gui/face-handle? face
	SetScrollPos hWnd 1 0 yes
	gui/SendMessage hWnd 00B6h 0 0							;-- EM_LINESCROLL
	gui/SendMessage hWnd 00B7h 0 0							;-- EM_SCROLLCARET
]

scroll-bottom: routine [
	face [object!]
	/local 
		hWnd	[handle!]
		pos-min [integer!]
		pos-max [integer!]
][
	hWnd: gui/face-handle? face
	pos-min: 0
	pos-max: 0
	GetScrollRange hWnd 1 :pos-min :pos-max
	SetScrollPos gui/face-handle? face 1 pos-max yes
	gui/SendMessage hWnd 00B6h 0 99999						;-- EM_LINESCROLL
	gui/SendMessage hWnd 00B7h 0 0							;-- EM_SCROLLCARET
]


text: {
This
is
a
very
long
text
to
make
the
vertical
scrollbar
appear
}

view [
	a: area 200x150 text
	panel [
		below
		button "top"	[scroll-top a]
		button "bottom" [scroll-bottom a]
	]
]
```

## Comments

No comments.
