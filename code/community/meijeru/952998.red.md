# FORM function in Red/System (improved version)

**Gist ID:** 952998
**Created:** 2011-05-03T08:20:34Z
**Last updated:** 2015-09-25T16:48:06Z

## File: `form.reds`

```Red
Red/System [
	Title:		"Form"
	Purpose:	{formats an integer in a c-string, as signed decimal or as hex}
	Author:		"Rudolf W. MEIJER"
	File:		%form-number.reds 
	Version:	0.7
	Date:		27-May-2011
	Notes: 		{Improved version, not final}
]

#include %stringf.reds

form-signed: func [
	i [integer!]
	return: [c-string!]
	/local s [c-string!] r [c-string!] c [integer!] n [logic!]
][
	if zero? i [return "0"]
	s: make-string 11
	n:  negative? i
	if n [i: negate i]
	c: 11
	while [i <> 0][
		s/c: #"0" + (i // 10)
		i: i / 10
		c: c - 1
	]
	if n [s/c: #"-" c: c - 1]
	r: copy-part s + c 11 - c
	free s
	r
]

form-hex: func [
	i [integer!]
	return: [c-string!]
	/local s [c-string!] r [c-string!] c [integer!] d [integer!]
][
	if zero? i [return "0"]
	s: make-string 8
	c: 8
	while [i <> 0][
		d: i // 16
		if d > 9 [d: d + 7]		; 7 = (#"A" - 1) - #"9"
		s/c: #"0" + d
		i: i / 16
		c: c - 1
	]
	r: copy-part s + c 8 - c
	free s
	r
]

```

## Comments

### @dockimbel on 2011-05-03T21:35:24Z

The code looks nice. You will be able to shorten it when c-string! arithmetic will be implemented (should be this week). About the lack of unary minus, there is the NEGATE macro defined in %common.reds which can be used as replacement for [0 - ]. So:

```
if i < 0 [n: true i: 0 - i]
```

can be written:

```
if negative? i [n: true i: negate i]
```

(NEGATIVE? is another macro)


### @meijeru on 2011-05-05T07:39:37Z

Thanks. Meanwhile I have defined a function abs which does the negation, and I have realised that the substring manipulation is not necessary at all. One can already just add an integer to a c-string! value which is in fact a pointer. See my new version.


### @meijeru on 2011-05-05T08:35:29Z

More simplfication in the calculation of the bytes for the individual digits. See latest version.


### @dockimbel on 2011-05-05T09:22:11Z

Good to know that c-string! arithmetic is already working. I will need to review the internal code paths and generated code to be sure it is working correctly.


### @meijeru on 2011-05-28T08:14:56Z

The newest version provides both decimal (signed) and hexadecimal (unsigned) formats. It uses memory allocation (calloc) and freeing (free) as provided in my other gist: stringf.reds which was insppired by Kaj de Vos' C-library. Please note that free can only free a block of memory previously allocated by malloc/calloc.


### @dockimbel on 2011-05-29T09:16:24Z

Function 'form-hex will be surely useful to print memory addresses for debugging purpose.


### @PeterWAWood on 2011-06-17T14:02:14Z

I suspect form-signed may give an incorrect result when printing -2147483648 as the number currently cannot be properly negated.


### @dockimbel on 2011-06-19T21:07:00Z

Peter is right, `form-signed -2147483648` gives `-./,),(-*,(` as result.


### @dockimbel on 2011-06-21T19:40:35Z

Rudolf, I did a quick patch in Red/System's runtime to cop with printing -2147483648 in commit [3424bcbb](https://github.com/dockimbel/Red/commit/3424bcbb5ca7c6b27f915f746d966ea963706df8) in the modified version of your `form-signed` function. If you have any proposition to improve my hardcoded version, let me know. 


