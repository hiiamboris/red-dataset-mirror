# 

**Gist ID:** 905d884b263e2b80050cc3f9051f86f9
**Created:** 2016-04-10T18:19:48Z
**Last updated:** 2016-07-14T02:38:31Z

## File: `desaturate.red`

```Red
Red [
   Title: "Dessaturate"
   Author: "Guaracy Monteiro"
   Needs: 'View
   Version: 0.1
]

desaturate: function [
   image
   /Lightness
   /Luminosity    
   /RedChannel
   /GreenChannel
   /BlueChannel
   /local img
   ][
   img: copy image
   case [
      Lightness [
	 forall img [
	    t: img/1
	    t/1: t/2: t/3: ((max max (t/1) (t/2) (t/3)) + (min min (t/1) (t/2) (t/3))) / 2
	    img/1: t
	 ]
      ]
      Luminosity [
	 forall img [
	    t: img/1
	    t/1: t/2: t/3: to integer! ((t/1 * 0.21) + (t/2 * 0.72) + (t/3 * 0.07))
	    img/1: t
	 ]
      ]
      RedChannel [
	 forall img [
	    t: img/1
	    t/2: t/3: t/1
	    img/1: t
	 ]
      ]
      GreenChannel [
	 forall img [
	    t: img/1
	    t/1: t/3: t/2
	    img/1: t
	 ]
      ]
      BlueChannel [
	 forall img [
	    t: img/1
	    t/1: t/2: t/3
	    img/1: t
	 ]
      ]
      true [  
	 forall img [
	    t: img/1
	    t/1: t/2: t/3: ((t/1 + t/2 + t/3) / 3)
	    img/1: t
	 ]
      ]
   ]
   return img
]

load-image: function [][
   request-file/title/filter "Load Image" ["Images" "*.jpg;*.png;*.bmp"]
]

im: load-image 

view [
   title "Desaturate image"
   text "Method:"
   return
   dd: drop-down
   on-change [
      im2/image:
      switch dd/selected [
	 1 [desaturate/Lightness im1/image]
	 2 [desaturate/Luminosity im1/image]
	 3 [desaturate im1/image]
	 4 [desaturate/RedChannel im1/image]
	 5 [desaturate/GreenChannel im1/image]
	 6 [desaturate/BlueChannel im1/image]
      ]
   ]
   data ["Lightness" "Luminosity" "Average" "Red Channel" "Green Channel" "Blue Channel"]
   button "Load" [
      im: load load-image
      im1/image: im
      im1/size: im/size
      im2/image: im
      im2/size: im/size
   ]
   button "Quit" [unview]     
   return
   im1: image im
   im2: image im
]
quit
```

## Comments

No comments.
